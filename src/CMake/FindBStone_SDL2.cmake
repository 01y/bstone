#
# Locate SDL2 library.
#
# User-provided/generated variables:
#    - BSTONE_SDL2_LIB_NAME - the name of SDL2 library (default: SDL2).
#    - BSTONE_SDL2_MAIN_LIB_NAME - the name of SDL2main library (default: SDL2main).
#
# Generated variables:
#    - BSTONE_SDL2_INCLUDE_DIR - path to SDL2 headers.
#    - BSTONE_SDL2_LIBRARY_DIR - path to SDL2 libraries.
#    - BSTONE_SDL2_VERSION - SDL2 detected version.
#    - BSTONE_SDL2_FOUND - set to TRUE if SDL2 found.
#
# Imported targets:
#    - BStone::SDL2
#


#
# Find threading library
#
find_package(Threads)


#
# Headers
#
find_path(
	BSTONE_SDL2_INCLUDE_DIR
		SDL.h
	HINTS
		PATH_SUFFIXES
			SDL2
			include/SDL2
)

unset(BSTONE_SDL2_TMP_ARE_INCS_FOUND)

if (BSTONE_SDL2_INCLUDE_DIR)
	set(
		BSTONE_SDL2_TMP_VERSION_H
		${BSTONE_SDL2_INCLUDE_DIR}/SDL_version.h
	)

	unset(BSTONE_SDL2_VERSION_LINE)

	if (EXISTS ${BSTONE_SDL2_TMP_VERSION_H})
		set(
			BSTONE_SDL2_TMP_DIGIT_REGEX
			"^[0-9]$"
		)

		set(
			BSTONE_SDL2_TMP_MAJOR_REGEX
			"^#define[ \t]+SDL_MAJOR_VERSION[ \t]+([0-9])$"
		)

		set(
			BSTONE_SDL2_TMP_MINOR_REGEX
			"^#define[ \t]+SDL_MINOR_VERSION[ \t]+([0-9])$"
		)

		set(
			BSTONE_SDL2_TMP_PATCH_REGEX
			"^#define[ \t]+SDL_PATCHLEVEL[ \t]+([0-9])$"
		)

		file(
			STRINGS
			${BSTONE_SDL2_TMP_VERSION_H}
			BSTONE_SDL2_MAJOR_VERSION_LINE
			REGEX ${BSTONE_SDL2_TMP_MAJOR_REGEX}
		)

		file(
			STRINGS
			${BSTONE_SDL2_TMP_VERSION_H}
			BSTONE_SDL2_MINOR_VERSION_LINE
			REGEX ${BSTONE_SDL2_TMP_MINOR_REGEX}
		)

		file(
			STRINGS
			${BSTONE_SDL2_TMP_VERSION_H}
			BSTONE_SDL2_PATCH_VERSION_LINE
			REGEX ${BSTONE_SDL2_TMP_PATCH_REGEX}
		)

		string(
			REGEX REPLACE
			${BSTONE_SDL2_TMP_MAJOR_REGEX}
			"\\1"
			BSTONE_SDL2_MAJOR_VERSION
			${BSTONE_SDL2_MAJOR_VERSION_LINE}
		)

		string(
			REGEX REPLACE
			${BSTONE_SDL2_TMP_MINOR_REGEX}
			"\\1"
			BSTONE_SDL2_MINOR_VERSION
			${BSTONE_SDL2_MINOR_VERSION_LINE}
		)

		string(
			REGEX REPLACE
			${BSTONE_SDL2_TMP_PATCH_REGEX}
			"\\1"
			BSTONE_SDL2_PATCH_VERSION
			${BSTONE_SDL2_PATCH_VERSION_LINE}
		)

		if (BSTONE_SDL2_MAJOR_VERSION MATCHES ${BSTONE_SDL2_TMP_DIGIT_REGEX} AND
			BSTONE_SDL2_MINOR_VERSION MATCHES ${BSTONE_SDL2_TMP_DIGIT_REGEX} AND
			BSTONE_SDL2_PATCH_VERSION MATCHES ${BSTONE_SDL2_TMP_DIGIT_REGEX}
			)
			set(
				BSTONE_SDL2_VERSION_LINE
				${BSTONE_SDL2_MAJOR_VERSION}.${BSTONE_SDL2_MINOR_VERSION}.${BSTONE_SDL2_PATCH_VERSION}
			)
		endif ()
	endif ()

	if (BSTONE_SDL2_VERSION_LINE)
		set(BSTONE_SDL2_TMP_ARE_INCS_FOUND TRUE)
		set(BSTONE_SDL2_VERSION ${BSTONE_SDL2_VERSION_LINE})
	endif ()
endif ()


#
# Libraries
#
unset(BSTONE_SDL2_TMP_IS_LIB_FOUND)
unset(BSTONE_SDL2_TMP_IS_MAIN_LIB_FOUND)
unset(BSTONE_SDL2_TMP_ARE_LIBS_FOUND)
unset(BSTONE_SDL2_TMP_FOUND_LIBS)
unset(BSTONE_SDL2_TMP_MISSING_LIBS)

if (NOT BSTONE_SDL2_LIB_NAME)
	set(BSTONE_SDL2_LIB_NAME SDL2 CACHE STRING "The name of SDL2 library.")
endif ()

if (NOT BSTONE_SDL2_MAIN_LIB_NAME)
	set(BSTONE_SDL2_MAIN_LIB_NAME SDL2main CACHE STRING "The name of SDL2main library.")
endif ()

find_library(
	BSTONE_SDL2_LIBRARY_DIR
	NAMES
		${BSTONE_SDL2_LIB_NAME}
	HINTS
		PATH_SUFFIXES
			lib
)

if (BSTONE_SDL2_LIBRARY_DIR)
	if (NOT IS_DIRECTORY ${BSTONE_SDL2_LIBRARY_DIR})
		get_filename_component(BSTONE_SDL2_LIBRARY_DIR ${BSTONE_SDL2_LIBRARY_DIR} DIRECTORY)
		set(BSTONE_SDL2_LIBRARY_DIR ${BSTONE_SDL2_LIBRARY_DIR} CACHE PATH "SDL2 library directory." FORCE)
	endif ()

	#
	# SDL2
	#
	set(
		BSTONE_SDL2_TMP_LIB
		${BSTONE_SDL2_LIBRARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${BSTONE_SDL2_LIB_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}
	)

	if (EXISTS ${BSTONE_SDL2_TMP_LIB})
		set(BSTONE_SDL2_TMP_IS_LIB_FOUND TRUE)
		list(APPEND BSTONE_SDL2_TMP_FOUND_LIBS ${BSTONE_SDL2_TMP_LIB})
	endif ()


	#
	# SDL2main
	#
	# FIXME Are all platforms has "SDL2main"?
	#
	set(
		BSTONE_SDL2_TMP_LIB
		${BSTONE_SDL2_LIBRARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${BSTONE_SDL2_MAIN_LIB_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}
	)

	if (EXISTS ${BSTONE_SDL2_TMP_LIB})
		set(BSTONE_SDL2_TMP_IS_MAIN_LIB_FOUND TRUE)
		list(APPEND BSTONE_SDL2_TMP_FOUND_LIBS ${BSTONE_SDL2_TMP_LIB})
	endif ()

	if (BSTONE_SDL2_TMP_IS_LIB_FOUND AND BSTONE_SDL2_TMP_IS_MAIN_LIB_FOUND)
		set(BSTONE_SDL2_ARE_LIBS_FOUND TRUE)
	endif ()

	if (BSTONE_SDL2_ARE_LIBS_FOUND)
		set(BSTONE_SDL2_LIBRARY ${BSTONE_SDL2_TMP_FOUND_LIBS})
	endif ()
endif ()

if (BSTONE_SDL2_TMP_ARE_INCS_FOUND AND BSTONE_SDL2_ARE_LIBS_FOUND)
	set(BSTONE_SDL2_FOUND TRUE)
else ()
	unset(BSTONE_SDL2_FOUND)
endif ()


#
# Default handler
#
include(FindPackageHandleStandardArgs)

find_package_handle_standard_args(
	BSTONE_SDL2
	REQUIRED_VARS
		BSTONE_SDL2_FOUND
		BSTONE_SDL2_VERSION
		BSTONE_SDL2_LIB_NAME
		BSTONE_SDL2_MAIN_LIB_NAME
		BSTONE_SDL2_INCLUDE_DIR
		BSTONE_SDL2_LIBRARY_DIR
	VERSION_VAR
		BSTONE_SDL2_VERSION
)


#
# Add imported target
#
if (BSTONE_SDL2_FOUND)
	set(BSTONE_SDL2_TMP_LIBRARIES "")

	if (MINGW)
		list(
			APPEND BSTONE_SDL2_TMP_LIBRARIES
			mingw32
		)
	endif ()

	list(
		APPEND BSTONE_SDL2_TMP_LIBRARIES
		${BSTONE_SDL2_LIBRARY}
		Threads::Threads
	)

	if (WIN32)
		list(
			APPEND BSTONE_SDL2_TMP_LIBRARIES
			imm32
			version
			winmm
		)
	endif ()


	add_library(BStone::SDL2 INTERFACE IMPORTED)

	set_property(
		TARGET BStone::SDL2
		PROPERTY
			INTERFACE_LINK_LIBRARIES
				${BSTONE_SDL2_TMP_LIBRARIES}
	)
endif ()
